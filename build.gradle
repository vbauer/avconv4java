
plugins {
    id 'java'
    id 'jacoco'
    id 'org.jetbrains.kotlin.jvm' version '1.3.21'
    id 'com.github.kt3k.coveralls' version '2.8.2'
    id 'com.github.ben-manes.versions' version '0.21.0'
    id 'ru.vyarus.quality' version '3.4.0'
    id 'ru.vyarus.java-lib' version '1.1.2'
}

group 'com.github.vbauer'
version '1.2.3'
description 'Pure-java interface to the avconv commandline'

repositories {
    mavenLocal()
    mavenCentral()
}

configurations.provided.extendsFrom configurations.annotationProcessor

ext.deps = [
    testng: '6.14.3',
    hamcrest: '2.1',
    jpcc: '1.2.0'
]

dependencies {
    testImplementation "org.testng:testng:${deps.testng}"
    testImplementation "org.hamcrest:hamcrest-library:${deps.hamcrest}"
    testImplementation "com.pushtorefresh.java-private-constructor-checker:checker:${deps.jpcc}"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

wrapper {
    gradleVersion = '5.3.1'
    distributionType = Wrapper.DistributionType.ALL
}

tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addBooleanOption('html5', true)
}

String javaVersion = JavaVersion.VERSION_1_8

compileJava {
    sourceCompatibility javaVersion
    targetCompatibility javaVersion
}

compileKotlin {
    kotlinOptions {
        jvmTarget = javaVersion
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = javaVersion
    }
}

test {
    useTestNG()
}

quality {
    pmd = false
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

pom {
    inceptionYear '2013'
    licenses {
        license {
            name 'Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0'
            distribution 'repo'
        }
    }
    developers {
        developer {
            id 'vbauer'
            name 'Vladislav Bauer'
            email 'bauer.vlad@gmail.com'
            url 'http://linkedin.com/in/vladislavbauer'
            roles {
                role 'Developer'
            }
        }
    }
    issueManagement {
        system 'GitHub Issues'
        url 'https://github.com/vbauer/avconv4java/issues'
    }
    ciManagement {
        system 'Travis'
        url 'https://travis-ci.org/vbauer/avconv4java'
    }
}
